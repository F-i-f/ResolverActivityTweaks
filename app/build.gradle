apply plugin: 'com.android.application'

def getGitRevision = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return "<unknown git revision>"
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.fifsource.android.resolveractivitytweaks"
        minSdkVersion 21
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 23
        def VERSIONCODE = 9
        versionCode VERSIONCODE
        versionName "2.1"
        def RANDOM_BUILD_CODE = "\""+VERSIONCODE+"/"+String.format("%X", new Random().nextLong())+"\""
        buildConfigField "String", "RANDOM_BUILD_CODE", RANDOM_BUILD_CODE
        buildConfigField "String", "GIT_REVISION", "\""+getGitRevision()+"\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:support-v4:23.4.0'
    compileOnly 'de.robv.android.xposed:api:81'
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}
